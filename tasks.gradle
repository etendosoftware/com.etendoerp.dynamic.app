/**
 * This script is used to create and build subapps for the dynamic app module.
 */
def yarnDir = {
    def targetDir = project.hasProperty("pkg") ? project.property("pkg") : null
    if (targetDir == null) {
        throw new GradleException("Target module is required. Please provide the module package using -Ppkg=module-name")
    }
    def moduleDir = new File("${rootDir}/modules/${targetDir}")
    if (!file(moduleDir).exists()) {
        throw new GradleException("Module directory does not exist. Please create the module and export first.")
    }
    return moduleDir
}

/**
 * Execute a command in the specified directory.
 */
def execYarn = { String workDir, String command ->
    println "Working directory: ${workDir}"
    println "Command: ${command}"
    def shell = 'sh'
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        shell = 'cmd.exe'
    }
    exec {
        workingDir workDir
        commandLine shell, "-c", command
    }
}

/**
 * Create a new subapp for the module.
 */
task "subapp.create" {
    finalizedBy "subapp.create.config"
    group = "subapp"
    description = "Create a new subapp for the module"
    doLast {
        def moduleDir = yarnDir()
        if (file("${moduleDir}/subapp").exists()) {
            throw new GradleException("Module already has a subapp")
        }
        execYarn moduleDir.toString(), 'npx react-native init subapp --template etendo-subapp-data-template-typescript --skip-install --skip-git-init'
    }
}

/**
 * Create a configuration file for the subapp.
 */
task "subapp.create.config" {
    group = "subapp"
    description = "Create a configuration file for the subapp. This task is automatically added to the subapp.create task."
    doLast {
        def targetDir = project.hasProperty("module") ? project.property("module") : null
        def moduleDir = new File("${rootDir}/modules/${targetDir}")
        def etendoConfig = new File("${moduleDir}/subapp/etendo.config.json")
        def path = "../web/${targetDir}"
        file(moduleDir).mkdirs()
        etendoConfig.write("""{
   "outputPath": "${path}",
   "bundleFileName": "dist.js"
}
""")
    }
}

task "subapp.build" {
    group = "subapp"
    description = "Build subapps for the module. This task is automatically added to the smartbuild task."
    dependsOn "copyCodeGenFiles"
    doLast {
        def dirs = ['modules']
        dirs.each { dir ->
            fileTree(dir: dir, include: "**/subapp/package.json").each { pkgFile ->
                {
                    def subappDir = pkgFile.parent
                    println("Building subapp in ${subappDir}")
                    execYarn subappDir.toString(), 'yarn install'
                    execYarn subappDir.toString(), 'yarn build'
                }
            }
        }
    }
}

task "subapp.dev" {
    group = "subapp"
    description = "Start development server for subapps"
    dependsOn "copyCodeGenFiles"
    doLast {
        def moduleDir = yarnDir()
        def subappDir = "${moduleDir}/subapp"
        if (!file("${subappDir}").exists()) {
            throw new GradleException("Module does not have a subapp")
        }
        execYarn subappDir, 'yarn install'
        execYarn subappDir, 'yarn build'
        execYarn subappDir, 'yarn dev'
    }
}

configurations {
    codegen
}


task downloadCodeGenJar {
    doLast {
        def outputDir = file("${buildDir}/dynapp/libs")
        def codegenDependency = "com.etendorx:generate-entities:${project.ext.ETENDORX_VERSION}"
        def dependency = dependencies.create(codegenDependency)
        def configuration = configurations.detachedConfiguration(dependency)
        configuration.resolve()
        configuration.files.each { file ->
            copy {
                from file
                into outputDir
                rename { 'app.jar' }
            }
        }
    }
}

task generateGradlePropertiesFile {
    def gradlePropertiesFile = file("${buildDir}/dynapp/gradle.properties")
    def dbHost = project.hasProperty("bbdd.host") ? project.property("bbdd.host") : "localhost"
    def dbPort = project.property("bbdd.port") ?: "5432"
    def dbSid = project.property("bbdd.sid") ?: "etendo"

    doLast {
        gradlePropertiesFile.parentFile.mkdirs()
        gradlePropertiesFile.text = """
            bbdd.rdbms=POSTGRE
            bbdd.driver=org.postgresql.Driver
            bbdd.url=jdbc:postgresql://${dbHost}:${dbPort}
            bbdd.sid=${dbSid}
            bbdd.systemUser=postgres
            bbdd.systemPassword=syspass
            bbdd.user=tad
            bbdd.password=tad
            bbdd.sessionConfig=select update_dateFormat('DD-MM-YYYY')
            rx.generateCode=true
        """.stripIndent()
    }

    outputs.file(gradlePropertiesFile)
}

task runCodeGenJar(type: Exec) {
    dependsOn "downloadCodeGenJar", "generateGradlePropertiesFile"

    workingDir "${buildDir}/dynapp"
    commandLine 'java', '-jar', 'libs/app.jar'

    inputs.file(file("${buildDir}/dynapp/gradle.properties"))
    outputs.upToDateWhen { false }
}

task copyCodeGenFiles {
    dependsOn "runCodeGenJar"

    doLast {
        if(!project.hasProperty("pkg")) {
            return
        }
        def sourceRootDir = file("${buildDir}/dynapp/null/")
        sourceRootDir.eachDir { moduleDir ->
            println "Copying codegen files for ${moduleDir}"
            def sourceDataGenDir = file("${moduleDir}/lib/data_gen")
            if (sourceDataGenDir.exists()) {
                def module = project.property("pkg")
                def subappDir = file("./modules/${module}/subapp")
                if (subappDir.exists()) {
                    def destinationDir = file("${subappDir}/src/lib/data_gen")
                    if (destinationDir.exists()) {
                        destinationDir.deleteDir()
                    }
                    copy {
                        from sourceDataGenDir
                        into destinationDir
                    }
                    println "Copied ${sourceDataGenDir} to ${destinationDir}"
                }
            }
        }
    }
}
